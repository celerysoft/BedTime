apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.celerysoft.bedtime"
        versionCode 22
        versionName "1.3.0"
        minSdkVersion 14
        targetSdkVersion 25
        manifestPlaceholders = [
                UMENG_APPKEY : UMENG_APPKEY_FOR_BED_TIME,
                TENCENT_AUTH_SCHEME : "tencent" + QQ_APP_ID_FOR_BED_TIME,
                WECHAT_APP_ID : WECHAT_APP_ID_FOR_BED_TIME,
                WECHAT_APP_SECRET : WECHAT_APP_SECRET_FOR_BED_TIME,
                QQ_APP_ID : QQ_APP_ID_FOR_BED_TIME,
                QQ_APP_KEY : QQ_APP_KEY_FOR_BED_TIME
        ]
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }

        release {
            // TODO (For open source developer only) replace to your own key store
            // if you have no idea what to do, just remove all 4 lines below.
            storeFile file("celerysoft.jks")
            storePassword CELERY_STORE_PASSWORD
            keyAlias CELERY_KEY_ALIAS
            keyPassword CELERY_KEY_PASSWORD
        }
    }
    
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " - Debug"
            signingConfig signingConfigs.debug

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('debug.apk')) {
                        def newName = "BedTime-${defaultConfig.versionName}-debug.apk"
                        output.outputFile = new File(outputFile.parent, newName)
                    }
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                        def newName = "BedTime-${defaultConfig.versionName}-release-${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, newName)
                    }
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    productFlavors {
        Wandoujia {
            manifestPlaceholders = [ UMENG_CHANNEL_NAME:"Wandoujia" ]
        }
        Yingyongbao {
            manifestPlaceholders = [ UMENG_CHANNEL_NAME:"Yingyongbao" ]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.wdullaer:materialdatetimepicker:2.3.0'
    compile 'com.celerysoft:rippletransitionanimationview:0.7.1'
    compile 'com.umeng.analytics:analytics:latest.integration'
}
